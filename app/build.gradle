apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 32

	defaultConfig {
        applicationId "org.andbootmgr.app"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 3001
        versionName "0.3.0-m0"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        debug {
            if (project.hasProperty('ABM_DEBUG_KEY_ALIAS')) {
                storeFile file(ABM_DEBUG_STORE_FILE)
                storePassword ABM_DEBUG_STORE_PASSWORD
                keyAlias ABM_DEBUG_KEY_ALIAS
                keyPassword ABM_DEBUG_KEY_PASSWORD
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
        release {
            if (project.hasProperty('ABM_RELEASE_KEY_ALIAS')) {
                storeFile file(ABM_RELEASE_STORE_FILE)
                storePassword ABM_RELEASE_STORE_PASSWORD
                keyAlias ABM_RELEASE_KEY_ALIAS
                keyPassword ABM_RELEASE_KEY_PASSWORD
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
	        if (project.hasProperty('ABM_RELEASE_KEY_ALIAS')) {
	    	    signingConfig signingConfigs.release
	        }
        }
        debug {
            if (project.hasProperty('ABM_DEBUG_KEY_ALIAS')) {
                signingConfig signingConfigs.debug
            } else if (project.hasProperty('ABM_RELEASE_KEY_ALIAS')) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
    }
    kotlinOptions {
        jvmTarget = '9'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.1.1"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
	namespace 'org.andbootmgr.app'
    applicationVariants.configureEach {
        tasks["merge${it.name.capitalize()}Assets"].dependsOn(setAssetTs)
    }
}

task setAssetTs(type: Task) {
    doLast {
        file("$rootDir/app/src/main/assets/cp/_ts").text = System.currentTimeMillis() / 1000L
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.4.2'
    implementation 'androidx.navigation:navigation-ui:2.4.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation "androidx.compose.ui:ui:1.1.1"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:1.1.1"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:1.1.1"
    // Material Design
    implementation "androidx.compose.material:material:1.1.1"
    implementation "androidx.compose.material3:material3:1.0.0-alpha12"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core:1.1.1"
    implementation "androidx.compose.material:material-icons-extended:1.1.1"
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0-rc01'
    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata:1.1.1"
    implementation "androidx.compose.runtime:runtime-rxjava2:1.1.1"

    implementation "androidx.navigation:navigation-compose:2.5.0-rc01"
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
	implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
	androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
	debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"


	def libsuVersion = '5.0.1'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"
    implementation "com.mikepenz:aboutlibraries-core:8.4.2"
    implementation "com.mikepenz:aboutlibraries:8.4.2"
}
